class TicTacToeGame {
	static Board board;
	static Piece nPiece,cPiece;
	static int callCounter;
	static boolean isDisplay;
	static int p1;
	
	function void init() {
		do InputUtil.init();
		let nPiece = Piece.new(1);
		let cPiece = Piece.new(2);
		return;
	}
	
	// 1-continue, 2-draw and 3-win;
	function int processBoard() {
		do board.evaluateBoard();
		if(board.isWin()) {
			do board.drawWin();
		}
		
		if(board.isWin()) {
			return 3;
		}
		if(board.isDraw()) {
			return 2;
		}
		
		return 1;
	}
	
	// 0-quit, 1-continue, 2-draw, 3-win
	function int play(int player,Piece p) {
		var boolean validPlayerInput;
		var int input;
		var Array playerMove;
		let validPlayerInput = false;
		
		if(player=0) { // human
		
			while(~validPlayerInput) { // the player input must be valid!
				let input = InputUtil.readInput("Player X : Enter move (1 to 9 or 0 to restart):");
				if(input=0) {
					return 0; // restart game!
				}
				let playerMove = TicTacToeGame.inputToMove(input);
				let validPlayerInput = board.isFree(playerMove[0],playerMove[1]);
			}
			
			do board.makeMove(playerMove[0],playerMove[1],p);
			do board.drawMove(playerMove[0],playerMove[1],p);
			do playerMove.dispose();
			return TicTacToeGame.processBoard();
		}else {

			while(~validPlayerInput) { // the player input must be valid!
				let input = InputUtil.readInput("Player O : Enter move (1 to 9 or 0 to restart):");
				if(input=0) {
					return 0; // restart game!
				}
				let playerMove = TicTacToeGame.inputToMove(input);
				let validPlayerInput = board.isFree(playerMove[0],playerMove[1]);
			}
			
			do board.makeMove(playerMove[0],playerMove[1],p);
			do board.drawMove(playerMove[0],playerMove[1],p);
			do playerMove.dispose();
			return TicTacToeGame.processBoard();
		}
	}
	
	function void startGame() {
		var int currentPlayer,currentPiece; //  0 for you and 1 for computer
		var boolean gameOver;
		var int gameStatus;
		do TicTacToeGame.showCredits();
		
		do InputUtil.showMessage("Press enter to start the game!");

		while(true) {
			let currentPlayer = InputUtil.waitForZeroOrOne("First move? (0 for Player X, 1 for Player O): ");
			let currentPiece = cPiece;
			if(currentPlayer=1) {
				let currentPiece = nPiece;
			}
		
			let board = Board.new(200,60);
			let gameOver = false;
			while(~gameOver) {
				let gameStatus = TicTacToeGame.play(currentPlayer,currentPiece);
				
				if(gameStatus=0) {
					let gameOver = true;
				}
				if(gameStatus=1) {
					let currentPlayer = TicTacToeGame.getOtherPlayer(currentPlayer);
					let currentPiece = TicTacToeGame.getOtherPiece(currentPiece);
				}
				if(gameStatus=2) {
					do InputUtil.showMessage("Game drawn! Press enter to restart!");
					do TicTacToeGame.wait();
					let gameOver = true;
				}
				if(gameStatus = 3) {
					if(currentPlayer=0) {
						do InputUtil.showMessage("Congratulations! Player X won the game!");
					}else {
						do InputUtil.showMessage("Congratulations! Player O won the game!");
					}
					do TicTacToeGame.wait();
					let gameOver = true;
				}
			}
			do board.dispose();// start with a new board!
		}
	
		return;
	}
	

	
	function int getOtherPiece(Piece p) {
		if(p = nPiece) {
			return cPiece;
		}else {
			return nPiece;
		}
	}
	
	function int getOtherPlayer(int p) {
		if(p=0) {
			return 1;
		}else {
			return 0;
		}
	}
	
	
	function Array inputToMove(int input) {
		var Array moves;
		let moves = Array.new(2);
		let moves[0] = (input-1)/3 ; //row;
		let moves[1] = (input-1)-(3*moves[0]);
		return moves;
		
	}
	

	function void wait() {
		var boolean enterPressed;
		let enterPressed = false;
		while(~enterPressed) {
			if(Keyboard.keyPressed()=128) {
				let enterPressed = true;
			}
		}
		return;
	}
	
	function void showCredits() {
		do Output.moveCursor(20,16);
		do Output.printString("Tic Tac Toe in Jack Language");
		do Output.moveCursor(21,22);
		do Output.printString("By Komal");
		return;
	}
	
}